buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.1.3"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.0"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 22
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        mavenCentral()
    }

    compile "com.android.support:support-annotations:22.0.0"
    compile "com.android.support:support-v4:22.0.0"
    compile "com.google.android.apps.muzei:muzei-api:2.0"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.squareup.okhttp:okhttp:2.3.0"
    compile "com.squareup.retrofit:retrofit:1.9.0"
}

task generateWrapper(type: Wrapper) {
    description "Generates Gradle wrapper."

    gradleVersion = "2.3"
}
