buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.5.0"
    }
}

apply plugin: "com.android.application"

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 2

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 21
        targetSdkVersion 28
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

static def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

static def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        jcenter()
        google()
    }

    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.core:core:1.0.2"
    implementation "com.google.android.apps.muzei:muzei-api:2.0"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.squareup.okhttp3:okhttp:4.1.0"
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
}
