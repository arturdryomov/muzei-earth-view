buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.2.3"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.0"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 2

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 22
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        mavenCentral()
    }

    compile "com.android.support:support-annotations:22.2.1"
    compile "com.android.support:support-v4:22.2.1"
    compile "com.google.android.apps.muzei:muzei-api:2.0"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.squareup.okhttp:okhttp:2.4.0"
    compile "com.squareup.retrofit:retrofit:1.9.0"
}
